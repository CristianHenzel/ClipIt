# -------------------------------------------------------------------------------
# Autoconf/automake.
# -------------------------------------------------------------------------------
AC_PREREQ([2.5])
AC_INIT([clipit], [1.4.3], [oss@rspwn.com])
AM_INIT_AUTOMAKE([AC_PACKAGE_TARNAME()], [AC_PACKAGE_VERSION()])

AC_CONFIG_MACRO_DIR([m4])

AC_CONFIG_SRCDIR([src/main.c])
AM_CONFIG_HEADER([config.h])

# -------------------------------------------------------------------------------
# Gettext.
# -------------------------------------------------------------------------------

GETTEXT_PACKAGE=clipit
AC_SUBST(GETTEXT_PACKAGE)
AC_DEFINE_UNQUOTED(GETTEXT_PACKAGE, "$GETTEXT_PACKAGE", [gettext package])

ALL_LINGUAS="cs da de es es_ES et fr hu it ja nb pl pl pt_BR ro ru sv tr zh_CN"
AM_GLIB_GNU_GETTEXT()
AC_PROG_INTLTOOL([0.23])

clipitlocaledir='${prefix}/${DATADIRNAME}/locale'
AC_SUBST(clipitlocaledir)

AM_GNU_GETTEXT([external])
AM_GNU_GETTEXT_VERSION(0.18.1)

# -------------------------------------------------------------------------------
# Application indicator.
# -------------------------------------------------------------------------------

APPINDICATOR_REQUIRED_VERSION=0.2.4

AC_ARG_ENABLE([appindicator],
	AS_HELP_STRING([--enable-appindicator[=@<:@no/auto/yes@:>@]],[Build support for application indicators]),
	[enable_appindicator=$enableval],
	[enable_appindicator="auto"])


if test x$enable_appindicator = xauto ; then
	PKG_CHECK_EXISTS(appindicator-0.1 >= $APPINDICATOR_REQUIRED_VERSION,
		[enable_appindicator="yes"],
		[enable_appindicator="no"])
fi

if test x$enable_appindicator = xyes ; then
	PKG_CHECK_MODULES(APPINDICATOR,
		[appindicator-0.1 >= $APPINDICATOR_REQUIRED_VERSION],
		[AC_DEFINE(HAVE_APPINDICATOR, 1, [Have AppIndicator])])
fi

AM_CONDITIONAL(HAVE_APPINDICATOR, test x$enable_appindicator = xyes)
AC_SUBST(APPINDICATOR_CFLAGS)
AC_SUBST(APPINDICATOR_LIBS)

AC_SUBST(X11_LIBS, -lX11)

# -------------------------------------------------------------------------------
# Config options.
# -------------------------------------------------------------------------------

AM_MAINTAINER_MODE([disable])

# -------------------------------------------------------------------------------
# Main settings.
# -------------------------------------------------------------------------------
# prefix=/usr

# -------------------------------------------------------------------------------
# Checks for programs.
# -------------------------------------------------------------------------------
AC_PROG_CC()
AC_PROG_INSTALL()
AC_PROG_MAKE_SET()

# -------------------------------------------------------------------------------
# Checks for libraries.
# -------------------------------------------------------------------------------
AM_PATH_GTK_2_0(2.10.0, [], AC_MSG_ERROR(Requirement not met: gtk >= 2.10.0))
AM_PATH_GLIB_2_0(2.14.0, [], AC_MSG_ERROR(Requirement not met: glib >= 2.14.0))

# -------------------------------------------------------------------------------
# Checks for header files.
# -------------------------------------------------------------------------------
AC_CHECK_HEADERS([stdlib.h string.h])

# -------------------------------------------------------------------------------
# Checks for typedefs, structures, and compiler characteristics.
# -------------------------------------------------------------------------------
AC_C_CONST()

# -------------------------------------------------------------------------------
# Checks for library functions.
# -------------------------------------------------------------------------------
AC_TYPE_SIGNAL()

AC_CONFIG_FILES([Makefile m4/Makefile
			doc/Makefile
			src/Makefile
			data/Makefile])

AC_OUTPUT([po/Makefile.in])
